from Standard.Base import all

import Standard.Base.Data.Locale
import Standard.Base.Data.Time
import Standard.Base.Data.Time.Date
import Standard.Base.Data.Time.Duration
import Standard.Base.Data.Time.Zone

polyglot java import java.time.format.DateTimeFormatter
polyglot java import java.time.Instant
polyglot java import java.time.LocalTime
polyglot java import org.enso.base.Time_Utils

## Obtains the current time from the system clock in the default time-zone.

   > Example
     Get the current time in the default time zone.

         import Standard.Base.Data.Time.Time_Of_Day

         example_now = Time_Of_Day.now
now : Time_Of_Day
now = Time_Of_Day LocalTime.now

## Obtains an instance of `Time_Of_Day` from an hour, minute, second
   and nanosecond.

   Arguments:
   - hour: The hour-of-day to represent, from 0 to 23.
   - minute: The minute-of-hour to represent, from 0 to 59.
   - second: The second-of-minute to represent, from 0 to 59.
   - nanosecond: The nano-of-second to represent, from 0 to 999,999,999.

   Returns a `Time_Error` if the provided time is not a valid time.

   > Example
     Create a new local time at Unix epoch.

         import Standard.Base.Data.Time.Time_Of_Day

         example_epoch = Time_Of_Day.new

   > Example
     Get the local time at 9:30.

         import Standard.Base.Data.Time.Time_Of_Day

         example_epoch = Time_Of_Day.new hour=9 minute=30
new : Integer -> Integer -> Integer -> Integer -> Time_Of_Day ! Time.Time_Error
new (hour = 0) (minute = 0) (second = 0) (nanosecond = 0) =
    Panic.recover (Time_Of_Day (LocalTime.of hour minute second nanosecond)) . catch e-> case e of
        Polyglot_Error err -> Error.throw (Time.Time_Error err.getMessage)
        x -> x

## Obtains an instance of `Time_Of_Day` from a text such as "10:15".

   Arguments:
   - text: The text to parse as a time of day.
   - pattern: The pattern to use for parsing the input text.
   - locale: The locale in which the pattern should be interpreted.

   Returns a `Time_Error` if the provided text cannot be parsed using the
   default format.

   ? Pattern Syntax
     For the list of accepted symbols in pattern refer to `Time.format` doc.

   ? Default Time Format
     The text must represent a valid time and is parsed using the ISO-8601
     extended local time format. The format consists of:

     - Two digits for the hour-of-day. This is pre-padded by zero to ensure two
       digits.
     - A colon
     - Two digits for the minute-of-hour. This is pre-padded by zero to ensure
       two digits.
     - If the second-of-minute is not available then the format is complete.
     - A colon
     - Two digits for the second-of-minute. This is pre-padded by zero to ensure
       two digits.
     - If the nano-of-second is zero or not available then the format is
       complete.
     - A decimal point
     - One to nine digits for the nano-of-second. As many digits will be output
       as required.

   > Example
     Get the time 15:05:30.

         import Standard.Base.Data.Time.Time_Of_Day

         example_parse = Time_Of_Day.parse "15:05:30"

   > Example
     Recover from the parse error.

         import Standard.Base.Data.Time
         import Standard.Base.Data.Time.Time_Of_Day

         example_parse = Time_Of_Day.parse "half twelve" . catch e-> case e of
             Time.Time_Error _ -> Time_Of_Day.new

   > Example
     Parse "04:30:20" as Time_Of_Day.

         import Standard.Base.Data.Time.Time_Of_Day

         example_parse = Time_Of_Day.parse "04:30:20" "HH:mm:ss"

   > Example
     Parse "4:30AM" as Time_Of_Day

         import Standard.Base.Data.Time.Time_Of_Day

         example_parse = Time_Of_Day.parse "4:30AM" "h:mma"
parse : Text -> Text | Nothing -> Locale -> Time_Of_Day ! Time.Time_Error
parse text pattern=Nothing locale=Locale.default =
    result = Panic.recover <| case pattern of
        Nothing -> LocalTime.parse text
        Text ->
            formatter = DateTimeFormatter.ofPattern pattern
            LocalTime.parse text (formatter.withLocale locale.java_locale)
        _ -> Panic.throw (Time.Time_Error "An invalid pattern was provided.")
    Time_Of_Day result . map_error <| case _ of
        Polyglot_Error err -> Time.Time_Error err.getMessage
        x -> x

type Time_Of_Day

    ## PRIVATE

       This type is a date-time object that represents a time, often viewed
       as hour-minute-second.

       Arguments:
       - internal_local_time: The internal representation of the time of day.

       Time is represented to nanosecond precision. For example, the value
       "13:45.30.123456789" can be stored in a `Time_Of_Day`.
    type Time_Of_Day internal_local_time

    ## Get the hour portion of the time of day.

       > Example
         Get the current hour.

             import Standard.Base.Data.Time.Time_Of_Day

             example_hour = Time_Of_Day.now.hour
    hour : Integer
    hour = this . internal_local_time . getHour

    ## Get the minute portion of the time of day.

       > Example
         Get the current minute.

             import Standard.Base.Data.Time.Time_Of_Day

             example_minute = Time_Of_Day.now.minute
    minute : Integer
    minute = this . internal_local_time . getMinute

    ## Get the second portion of the time of day.

       > Example
         Get the current second.

             import Standard.Base.Data.Time.Time_Of_Day

             example_second = Time_Of_Day.now.second
    second : Integer
    second = this . internal_local_time . getSecond

    ## Get the nanosecond portion of the time of day.

       > Example
         Get the current nanosecond.

             import Standard.Base.Data.Time.Time_Of_Day

             example_nanosecond = Time_Of_Day.now.nanosecond
    nanosecond : Integer
    nanosecond = this . internal_local_time . getNano

    ## Extracts the time as the number of seconds, from 0 to 24 * 60 * 60 - 1.

       > Example
         Convert the current time into elapsed seconds in the day.

             import Standard.Base.Data.Time.Time_Of_Day

             example_to_seconds = Time_Of_Day.now.to_seconds
    to_seconds : Integer
    to_seconds = this . internal_local_time . toSecondOfDay

    ## Combine this time of day with a date to create a point in time.

       Arguments:
       - date: The date on which this time should occur.
       - zone: The time-zone in which the time is specified.

       > Example
         Convert local time to 1st January 2020 12:30 at system timezone.

             import Standard.Base.Data.Time.Date
             import Standard.Base.Data.Time.Time_Of_Day

             example_to_time = Time_Of_Day.new 12 30 . to_time (Date.new 2020)
    to_time : Date -> Zone -> Time
    to_time date (zone = Zone.system) =
        Time.time (this . internal_local_time . atDate date.internal_local_date . atZone zone.internal_zone_id)

    ## Add the specified amount of time to this instant to get a new instant.

       Arguments:
       - amount: The amount of time to add to this instant.

       > Example

             import Standard.Base.Data.Time.Duration
             import Standard.Base.Data.Time.Time_Of_Day

             example_plus = Time_Of_Day.new + 3.seconds
    + : Duration -> Time_Of_Day
    + amount = if amount.is_date then Error.throw (Time.Time_Error "Time_Of_Day does not support date intervals") else
        Time_Of_Day (this . internal_local_time . plus amount.internal_duration)

    ## Subtract the  specified amount of time from this instant to get a new
       instant.

       Arguments:
       - amount: The amount of time to subtract from this instant.

       > Example
         Subtract 12 hours from a local time.

             import Standard.Base.Data.Time.Duration
             import Standard.Base.Data.Time.Time_Of_Day

             example_minus = Time_Of_Day.now - 12.hours
    - : Duration -> Time_Of_Day
    - amount = if amount.is_date then Error.throw (Time.Time_Error "Time_Of_Day does not support date intervals") else
        Time_Of_Day (this . internal_local_time . minus amount.internal_duration)

    ## Format this time of day as text using the default formatter.

       > Example
         Convert the current time to text.

             import Standard.Base.Data.Time.Time_Of_Day

             example_to_text = Time_Of_Day.now.to_text
    to_text : Text
    to_text = Time_Utils.default_time_of_day_formatter . format this.internal_local_time

    ## A Time_Of_Day to Json conversion.

       > Example
         Convert the current time to JSON.

             import Standard.Base.Data.Time.Time_Of_Day

             example_to_json = Time_Of_Day.now.to_text
    to_json : Json.Object
    to_json = Json.from_pairs [["type", "Time_Of_Day"], ["hour", this.hour], ["minute", this.minute], ["second", this.second], ["nanosecond", this.nanosecond]]

    ## Format this time of day using the provided formatter pattern.

       Arguments:
       - pattern: The pattern specifying how to format the time of day.

       ? Pattern Syntax
         Patterns are based on a simple sequence of letters and symbols. For
         example, "HH-mm-ss.SSS" will format "16:21:10" as "16-21-10.323".

         For the list of accepted symbols in pattern refer to the
         `Base.Data.Time.format` doc.

       > Example
         Format "16:21:10" as "16:21:00.1234"

             import Standard.Base.Data.Time.Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "HH:mm:ss.SSSS"

       > Example
         Format "16:21:10" as "16:21:00.123456789"

             import Standard.Base.Data.Time.Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "HH:mm:ss.n"

       > Example
         Format "16:21:10" as "4:21pm"

             import Standard.Base.Data.Time.Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "h:mma"

       > Example
         Format "16:21:10" as "04:21:10pm"

             import Standard.Base.Data.Time.Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "hh:mm:ssa"

       > Example
         Format "16:21:10" as "hour:4"

             import Standard.Base.Data.Time.Time_Of_Day

             example_format = Time_Of_Day.new 16 21 10 . format "'hour:'h"
    format : Text -> Text
    format pattern =
        DateTimeFormatter.ofPattern pattern . format this.internal_local_time

