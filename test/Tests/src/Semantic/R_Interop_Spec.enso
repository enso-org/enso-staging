from Standard.Base import all

import Standard.Test

foreign js my_method a b = """
    return a + b;

type My_Type
    type My_Type a b

    foreign r my_method = """
        this$a + this$b

    my_method_2 x = this.my_method * x

    foreign r my_method_3 y = """
        r <- this@my_method_2(y)
        r + 1

    foreign r my_throw = """
        stop("error in R code!", call.=FALSE)

    do_throw = Panic.throw "error!"

    foreign r do_catch = """
        tryCatch(this@do_throw(), error = function(e) e$message)

foreign r make_object = """
    x = 10
    list(x=x, y=FALSE, compare=function(guess) x < guess)

foreign r make_array = """
    list(list(x=10), list(x=20), list(x=30))

foreign r make_str str = """
    paste("foo", str, "bar", sep=" ")

foreign r does_not_parse = """
    @@ x y z foo

foreign r make_int = """
    10

foreign r make_double = """
    10.5

spec =
    pending = if Polyglot.is_language_installed "R" then Nothing else """
        Can't run R tests, R is not installed.
    Test.group "Polyglot R" pending=pending <|
        Test.specify "should allow declaring module-level methods in R" <|
            here.my_method 1 2 . should_equal 3

        Test.specify "should allow mutual calling of instance-level methods" <|
            My_Type 3 4 . my_method_3 5 . should_equal 36

        Test.specify "should expose methods and fields of R objects" <|
            obj = here.make_object
            obj.x . should_equal 10
            obj.y . should_be_false
            obj.compare 5 . should_be_false
            obj.compare 11 . should_be_true

        Test.specify "should expose array interfaces for R arrays" <|
            vec = Vector.Vector here.make_array
            vec.map .x . should_equal [10, 20, 30]

        Test.specify "should correctly marshall strings" <|
            str = here.make_str "x" + " baz"
            str.should_equal "foo x bar baz"

        Test.specify "should make R strings type pattern-matchable" <|
            str = here.make_str "x"
            t = case str of
                Text -> True
                _ -> False
            t.should_be_true

        Test.specify "should make R numbers type pattern-matchable" <|
            int_match = case here.make_int of
                Integer -> True
            int_match.should_be_true
            double_match = case here.make_double of
                Decimal -> True
            double_match.should_be_true
            num_int_match = case here.make_int of
                Number -> True
            num_int_match.should_be_true
            num_double_match = case here.make_double of
                Number -> True
            num_double_match.should_be_true

        Test.specify "should allow Enso to catch R exceptions" <|
            value = My_Type 1 2
            result = Panic.recover <| value.my_throw
            err = result.catch
            err.to_display_text.should_equal "Polyglot error: Error: error in R code!"

        pending="R does not support catching polyglot exceptions"
        Test.specify "should allow R to catch Enso exceptions" pending=pending <|
            value = My_Type 7 2
            result = value.do_catch
            result . should_equal 7

        Test.specify "should properly report parse errors" <|
            err = Panic.recover here.does_not_parse
            err.catch.to_display_text.should .contain 'parse exception'
